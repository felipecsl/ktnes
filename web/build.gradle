apply plugin: 'kotlin-multiplatform'
apply plugin: 'org.jetbrains.kotlin.frontend'

kotlinFrontend {
  downloadNodeJsVersion = "latest"

  webpackBundle {
    bundleName = "main"
    contentPath = file('src/jsMain/web')
    webpackConfigFile = file('webpack.config.js').absolutePath
    sourceMapEnabled = true
    mode = "development"
  }

  npm {
    dependency "react"
    dependency "core-js"
    dependency "react-dom"
    dependency "react-router-dom"
    dependency "kotlin"
    // This looks like an unofficial package but I couldn't find the actual Jetbrains one, only kotlinx-html
    dependency "kotlinx-html-js"
    dependency "@jetbrains/kotlin-react"
    dependency "@jetbrains/kotlin-react-dom"
    dependency "@jetbrains/kotlin-extensions"
    dependency "@jetbrains/kotlin-react-router-dom"
  }
}

kotlin {
  sourceSets {
    jsMain {
      dependencies {
        implementation project(':lib')
        implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
        // https://bintray.com/kotlin/kotlin-js-wrappers/kotlin-react
        implementation "org.jetbrains:kotlin-react:16.6.0-pre.71-kotlin-$kotlin_version"
        // https://bintray.com/kotlin/kotlin-js-wrappers/kotlin-react-dom
        implementation "org.jetbrains:kotlin-react-dom:16.6.0-pre.71-kotlin-$kotlin_version"
        // https://bintray.com/kotlin/kotlin-js-wrappers/kotlin-react-router-dom
        implementation "org.jetbrains:kotlin-react-router-dom:4.3.1-pre.71-kotlin-$kotlin_version"
        // https://github.com/Kotlin/kotlinx.html/releases
        implementation "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"
      }
    }
  }

  targets {
    fromPreset(presets.js, 'js') {
      configure([compilations.main, compilations.test]) {
        tasks.getByName(compileKotlinTaskName).kotlinOptions {
          metaInfo = true
          outputFile = file("build/js/${project.name}.js")
          sourceMap = true
          moduleKind = 'commonjs'
        }
      }

      configure(compilations.main) {
        tasks.getByName(compileKotlinTaskName).kotlinOptions {
          main = "call"
        }
      }
    }
  }
}

task copyLibs(type: Copy) {
  destinationDir buildDir

  into("js") {
    from project(':lib').file("build/js")
  }
}

bundle {
  dependsOn copyLibs
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}